// minimal shader for testing
#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_GOOGLE_include_directive : require

#include "common.glsl"

const float PI = 3.14159265;

layout(binding = 0, set = 0, rgba32f) uniform writeonly image2D displayImage;
layout(binding = 1, set = 0, rgba32f) uniform image2D accumulationImage;
layout(binding = 2, set = 0) uniform accelerationStructureEXT TLAS;

layout(location = 0) rayPayloadEXT Payload payload;
layout(location = 1) rayPayloadEXT bool inShadow;

struct Camera {
    vec3 origin;
    vec3 lower_left_corner;
    vec3 horizontal;
    vec3 vertical;
};

layout(push_constant, scalar) uniform cam {
    Camera camera;
    uint numAccumulatedFrames;
};
uint stepRNG(uint rngState) {
    return rngState * 747796405 + 1;
}

float stepAndOutputRNGFloat(inout uint rngState) {
    rngState = stepRNG(rngState);
    uint word = ((rngState >> ((rngState >> 28) + 4)) ^ rngState) * 277803737;
    word = (word >> 22) ^ word;
    return float(word) / 4294967295.0;
}

vec3 randomDirection(inout uint rngState) {
    const float theta = 2 * PI * stepAndOutputRNGFloat(rngState);
    const float u = 2.0 * stepAndOutputRNGFloat(rngState) - 1.0;
    const float r = sqrt(1.0 - u * u);
    return vec3(r * cos(theta), r * sin(theta), u);
}

vec2 randomGaussian(inout uint rngState) {
    const float u1 = max(1e-38, stepAndOutputRNGFloat(rngState));
    const float u2 = stepAndOutputRNGFloat(rngState);
    const float r = sqrt(-2.0 * log(u1));
    const float theta = 2 * PI * u2;
    return r * vec2(cos(theta), sin(theta));
}

vec3 generateDir(Camera camera, inout uint rngState) {
    vec2 randomCenter = vec2(0.5) + 0.25 * randomGaussian(rngState);
    vec2 uv = (vec2(gl_LaunchIDEXT.xy) + randomCenter) / vec2(gl_LaunchSizeEXT.xy);
    uv.y -= 1;
    uv.y *= -1;

    return camera.lower_left_corner + uv.x * camera.horizontal + uv.y * camera.vertical - camera.origin;
}

// should this be a uniform?
const vec3 SUN_DIR = vec3(-0.494118, 0.406275, -0.768628);

// explicitly sample our light sources (currently just the sun)
vec3 sampleLights() {
    inShadow = true;
    if (dot(payload.normal, SUN_DIR) > 0.0) { 
        uint flags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT;
        traceRayEXT(TLAS, flags, 0xFF, 0, 0, 1, payload.point, 0.001, SUN_DIR, 10000.0, 1);
    }
    if (!inShadow) {
        return vec3(1.0, 1.0, 0.98) * payload.attenuation;
    } else {
        return vec3(0.0);
    }
}

void main() {
    uint rngState = (numAccumulatedFrames + 1) * gl_LaunchSizeEXT.x * gl_LaunchIDEXT.x + gl_LaunchIDEXT.y;

    // the result that we write to our buffer
    vec3 accumulatedColor = vec3(0.0);

    const uint MAX_BOUNCES = 16;
    const uint SAMPLES_PER_FRAME = 2;
    for (uint sampleCount = 0; sampleCount < SAMPLES_PER_FRAME; sampleCount++) {
        vec3 throughput = vec3(1.0);
        
        // set up initial directions for first bounce
        vec3 direction = normalize(generateDir(camera, rngState));
        vec3 origin = camera.origin;

        for (uint bounceCount = 0; bounceCount < MAX_BOUNCES; bounceCount++) {
            if (bounceCount == MAX_BOUNCES) {
                break;
            }
            
            traceRayEXT(TLAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, origin, 0.0001, direction, 10000.0, 0);

            // if finished, add final bounce color to accumulated
            if (payload.done) {
                accumulatedColor += throughput * payload.attenuation;
                break;
            }

            // explicitly sample our lights; still unsure if this is correct
            // as we get weird super light halo underneath
            accumulatedColor += throughput * sampleLights();
            
            // set up info for next bounce
            origin = payload.point;
            vec3 normal = faceforward(payload.normal, direction, payload.normal);
            direction = normalize(normal + randomDirection(rngState));
            throughput *= payload.attenuation;
        }
    }

    ivec2 imageCoords = ivec2(gl_LaunchIDEXT.xy);
    vec3 previouslyAccumulated;
    if (numAccumulatedFrames == 0) {
        previouslyAccumulated = vec3(0);
    } else {
        previouslyAccumulated = imageLoad(accumulationImage, imageCoords).rgb;
    }
    imageStore(accumulationImage, imageCoords, vec4(previouslyAccumulated + accumulatedColor, 1.0));
    imageStore(displayImage, imageCoords, vec4((previouslyAccumulated + accumulatedColor) / ((numAccumulatedFrames + 1) * SAMPLES_PER_FRAME), 1.0));
}
